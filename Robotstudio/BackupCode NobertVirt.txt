MODULE ExternalGuidedMotion
    
    VAR egmident egmID1;
    VAR egmstate egmSt1;
    
        
    
    CONST egm_minmax egm_minmax_lin1:=[-1,1]; !in mm
    CONST egm_minmax egm_minmax_rot1:=[-2,2];! in degees
    CONST num X:= 83.0;
    CONST num Y:= -716.0;
    CONST num Z:= 700.0;
   
    VAR robtarget startPoint:=[[X,Y,Z],[0.369643972,0.239117982,0.892398933,-0.099045793],[-1,-2,0,1],[9E9,9E9,9E9,9E9,9E9,9E9]];
    VAR pose posecorTopOfRamp:=[[X,Y,Z],[0.207675184,-0.457027319,0.788402602,-0.355553631]];
    
  
  
    TASK PERS tooldata tSuctionCup:=[TRUE,[[120,0,57],[0.707106624,-0.000000155,-0.000000251,0.707106939]],[1,[0,0,1],[1,0,0,0],0,0,0]];
    VAR pose object; 
    TASK PERS wobjdata TopOfRamp:=[FALSE,TRUE,"",[[0,0,0],[1,0,0,0]],[[83, -716, 700],[0.408218,0.23457,0.875426,-0.109382]]];
    

    PROC main()
        object.rot:= OrientZYX(-30, 130, 0); !In relation to the base of the robot. Y is rotated 90 degrees + angle
        TopOfRamp:= [FALSE,TRUE,"",[[0,0,0],[1,0,0,0]],[[X,Y,Z], object.rot]];
        posecorTopOfRamp.rot:= object.rot;
        startPoint.rot:= object.rot;
        
        MoveJ startPoint,v500,fine,tSuctionCup;
        SetUpEGM;
    ENDPROC
  
    
    PROC SetUpEGM()
        EGMReset egmID1;
        EGMGetId egmID1;
        egmSt1 := EGMGetState(egmID1);
        TPWrite "EGM state: "\Num := egmSt1;

        IF egmSt1 <= EGM_STATE_CONNECTED THEN
            ! Set up the EGM data source: UdpUc server using device "EGMsensor:"and configuration "default"
            EGMSetupUC ROB_1, egmID1, "default", "EGMSensor:" \pose;
        ENDIF
        
        !Which program to run
        runEGM;

        IF egmSt1 = EGM_STATE_CONNECTED THEN
            TPWrite "Reset EGM instance egmID1";
            EGMReset egmID1;    
        ENDIF        
    ENDPROC
        
        
    PROC runEGM()
        EGMActPose egmID1\Tool:=tSuctionCup \WObj:=TopOfRamp,posecorTopOfRamp ,EGM_FRAME_WORLD, posecorTopOfRamp, EGM_FRAME_WORLD
        \x:=egm_minmax_lin1 \y:=egm_minmax_lin1 \z:=egm_minmax_lin1
        \rx:=egm_minmax_rot1 \ry:=egm_minmax_rot1 \rz:=egm_minmax_rot1\LpFilter:=5\Samplerate:=4\MaxSpeedDeviation:= 5000;
                
        EGMRunPose egmID1, EGM_STOP_HOLD\x \y \z\CondTime:=20 \RampInTime:=2\RampOutTime:=0.05;
        egmSt1:=EGMGetState(egmID1);   
    ENDPROC
   
 
ENDMODULE